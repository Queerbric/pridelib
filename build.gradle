plugins {
	id 'fabric-loom' version '0.12.+'
	id 'io.github.juuxel.loom-quiltflower' version '1.7.+'
	id 'org.quiltmc.quilt-mappings-on-loom' version '4.0.0'
	id 'java-library'
	id 'maven-publish'
}

archivesBaseName = project.archives_base_name
version = "${project.mod_version}+${getMCVersionString()}"
group = project.maven_group

// This field defines the Java version your mod target.
def targetJavaVersion = 8

boolean isMCVersionNonRelease() {
	return project.minecraft_version.matches('^\\d\\dw\\d\\d[a-z]$')
			|| project.minecraft_version.matches('\\d+\\.\\d+-(pre|rc)(\\d+)')
}

String getMCVersionString() {
	if (isMCVersionNonRelease()) {
		return project.minecraft_version
	}
	def version = project.minecraft_version.split('\\.')
	return version[0] + '.' + version[1]
}

repositories {
	maven {
		name = "legacy-fabric"
		url = "https://maven.legacyfabric.net"
	}
	maven { url 'https://jitpack.io' }
	mavenCentral()
}

loom {
	setIntermediaryUrl('https://maven.legacyfabric.net/net/fabricmc/intermediary/%1$s/intermediary-%1$s-v2.jar')

	runs {
		client {
			programArgs "--userProperties", "{}"
		}
	}
}

dependencies {
	minecraft "com.mojang:minecraft:${project.minecraft_version}"
	mappings "net.legacyfabric:yarn:${project.minecraft_version}+build.${project.yarn_mappings}:v2"
	modImplementation "net.fabricmc:fabric-loader:${project.loader_version}"

	modImplementation ("net.legacyfabric.legacy-fabric-api:legacy-fabric-api:${fabric_api_version}") {
		exclude module: "legacy-fabric-entity-events-v1"
	}

	modImplementation include('com.github.moehreag:search-in-resources:e5568c34d5')

	implementation include('it.unimi.dsi:fastutil:8.5.8') // 1.8.9 does not use fastutil by default
	implementation('org.apache.logging.log4j:log4j-core:2.19.0')
}

java {
	sourceCompatibility = JavaVersion.toVersion(targetJavaVersion)
	targetCompatibility = JavaVersion.toVersion(targetJavaVersion)

	withSourcesJar()
}

tasks.withType(JavaCompile).configureEach {
	it.options.encoding = 'UTF-8'

	it.options.release.set(targetJavaVersion)
}

processResources {
	inputs.property 'version', project.version

	filesMatching('fabric.mod.json') {
		expand 'version': project.version
	}
}

jar {
	from('LICENSE') {
		rename { "${it}_${project.archivesBaseName}" }
	}
}

// configure the maven publication
publishing {
	publications {
		mavenJava(MavenPublication) {
			from components.java

			pom {
				name = 'Pride Lib'
				description = 'A unified library for data-driven flags'
			}
		}
	}

	repositories {
		mavenLocal()
		maven {
			name 'BuildDirLocal'
			url "$buildDir/repo"
		}

		def prideLibMaven = System.getenv('PRIDELIB_MAVEN')
		if (prideLibMaven) {
			maven {
				name 'PrideLibMaven'
				url uri(prideLibMaven)
				credentials {
					username = project.findProperty('gpr.user') ?: System.getenv('MAVEN_USERNAME')
					password = project.findProperty('gpr.key') ?: System.getenv('MAVEN_PASSWORD')
				}
			}
		}
	}
}
